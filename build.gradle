import de.schablinski.gradle.activejdbc.ActiveJDBCInstrumentation

buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.31"
    }
}

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
    id "de.schablinski.activejdbc-gradle-plugin" version "1.4-beta.1"
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

// For using the latest snapshot version of ActiveJDBC instrumentation
activejdbc.toolVersion = '2.3'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31"
    activejdbc "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.31"
    compile group: 'org.javalite', name: 'activejdbc', version: '2.3'
    compile group: 'org.javalite', name: 'activejdbc-kt', version: '2.3'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.5'
    runtime 'com.h2database:h2:1.4.196'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task run(dependsOn: 'build', type: JavaExec) {
    main = 'activejdbc.examples.kotlin.SimpleExampleKt'
    classpath = sourceSets.main.runtimeClasspath
}


compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task instrumentKotlinModels(type: ActiveJDBCInstrumentation) {
    group = 'build'
    // TODO: find out kotlin plugin API for output dir
    classesDir = "${project.buildDir}/classes/kotlin/main"
}

tasks['compileKotlin'].doLast {
    instrumentKotlinModels.instrument()
}
